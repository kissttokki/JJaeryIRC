[script]
n0=;System Info by Ford_Lawnmower irc.mindforge.org #USA-Chat
n1=menu channel,status {
n2=  System Info
n3=  .$iif(!$chan,$style(2)) 전체채팅
n4=  ..ALL: allinfo say
n5=  ..OS-Memory: say $osinfo  
n6=  ..마더보드-CPU: say $cpuinfo $moboinfo
n7=  ..Network: say $netinfo
n8=  ..Video: say $videoinfo
n9=  ..Disk Drives: say $diskinfo
n10=  .개인에게만
n11=  ..All: allinfo echo
n12=  ..OS-Memory: echo -a $osinfo
n13=  ..마더보드-CPU: echo -a $cpuinfo $moboinfo
n14=  ..Network: echo -a $netinfo
n15=  ..Video: echo -a $videoinfo
n16=  ..Disk Drives: echo -a $diskinfo
n17=}
n18=alias allinfo {
n19=  $1- 04컴퓨터14 정보: $osinfo $diskinfo
n20=  $1- 04컴퓨터14 사양: $videoinfo $moboinfo $netinfo
n21=}
n22=alias -l cpuinfo {
n23=  .comopen Processor WbemScripting.SWbemLocator
n24=  if (!$comerr) {
n25=    if ($com(Processor, ConnectServer, 3, dispatch* Specs)) {
n26=      if ($com(Specs, Get, 3, string, Win32_Processor.DeviceID='CPU0', dispatch* Results)) {
n27=        var %manufacturer, %name, %caption, %clockspeed, %load, %architecture
n28=        if ($com(Results, Manufacturer, 3)) { %manufacturer = $com(Results).result }
n29=        if ($com(Results, Name, 3)) { %name = $com(Results).result }
n30=        if ($com(Results, Caption, 3)) { %caption = $com(Results).result }
n31=        if ($com(Results, CurrentClockSpeed, 3)) { %clockspeed = $com(Results).result }
n32=        if ($com(Results, LoadPercentage, 3)) { %load = $com(Results).result }
n33=        if ($com(Results, Architecture, 3)) { %architecture = $com(Results).result }
n34=        .comclose Results
n35=      }
n36=      .comclose Specs
n37=    }
n38=    .comclose Processor
n39=    $iif($isid,return,$iif(# ischan,say,echo -a)) 04C14PU:04 %manufacturer %name  %caption $+(%load,%) Load
n40=  }
n41=  else { echo -st Com Error $nopath($script) | .comclose Processor | return }
n42=}
n43=alias -l osinfo {
n44=  if (!$com(Wbem.2)) { WbemOpen }
n45=  var %TotalMemory 04T14otal 04M14emory:04 $round($calc($WbemGet(Win32_OperatingSystem,TotalVisibleMemorySize,1)/1024),2)
n46=  var %FreeMemory 04A14vailable 04M14emory:04 $round($calc($WbemGet(Win32_OperatingSystem,FreePhysicalMemory,1)/1024),2)
n47=  var %OSCaption 04O14S:04 $WbemGet(Win32_OperatingSystem,Caption,1)
n48=  var %OSVersion $WbemGet(Win32_OperatingSystem,Version,1)
n49=  var %OSArchitecture $WbemGet(Win32_OperatingSystem,OSArchitecture,1)
n50=  if ($com(Wbem.2)) { .comclose Wbem.2 }
n51=  $iif($isid,return,$iif(# ischan,say,echo -a)) %OSCaption %OSVersion %OSArchitecture 04U14ptime:04 $uptime(system,1) $+(%TotalMemory,MB) $+(%FreeMemory,MB)
n52=}
n53=alias -l moboinfo {
n54=  if (!$com(Wbem.2)) { WbemOpen }
n55=  var %Aname 04A14udio:04 $WbemGet(Win32_SoundDevice,Name,1)  
n56=  var %Description $WbemGet(Win32_BaseBoard,Description,1)
n57=  var %Manufacturer $WbemGet(Win32_BaseBoard,Manufacturer,1)
n58=  if ($com(Wbem.2)) { .comclose Wbem.2 }
n59=  $iif($isid,return,$iif(# ischan,say,echo -a)) 04M14OBO:04 %Manufacturer %Description %Aname 
n60=}
n61=alias -l netinfo {
n62=  if (!$com(Wbem.2)) { WbemOpen }
n63=  var %BytesReceived $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,BytesReceivedPerSec,1)
n64=  %BytesReceived = 04R14eceived:04 $bytes($calc(%BytesReceived + $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,BytesReceivedPerSec,2))).suf
n65=  var %BytesSent $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,BytesSentPerSec,1)
n66=  %BytesSent = 04S14ent:04 $bytes($calc(%BytesSent + $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,BytesSentPerSec,2))).suf
n67=  var %BytesTotal $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,BytesTotalPerSec,1)
n68=  %BytesTotal = 04T14otal:04 $bytes($calc(%BytesTotal + $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,BytesTotalPerSec,2))).suf
n69=  var %Bandwidth $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,CurrentBandwidth,1)
n70=  %Bandwidth = 04B14andwidth:04 $calc(%Bandwidth + $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,CurrentBandwidth,2)/100000)
n71=  var %Name $WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,Name,1)
n72=  %Name = $+(%Name,14/04,$WbemGet(Win32_PerfRawData_Tcpip_NetworkInterface,Name,2))
n73=  if ($com(Wbem.2)) { .comclose Wbem.2 }
n74=  $iif($isid,return,$iif(# ischan,say,echo -a)) 04N14etwork:04 %Name $+(%Bandwidth,KBps) %BytesReceived %BytesSent %BytesTotal 
n75=}
n76=alias -l videoinfo {
n77=  if (!$com(Wbem.2)) { WbemOpen }
n78=  var %Compatibility $WbemGet(Win32_VideoController,AdapterCompatibility,1)
n79=  var %VideoProcessor $WbemGet(Win32_VideoController,VideoProcessor,1)
n80=  var %AdapterRam $WbemGet(Win32_VideoController,AdapterRam,1)
n81=  var %Horizontal $WbemGet(Win32_VideoController,currenthorizontalresolution,1)
n82=  var %Vertical $WbemGet(Win32_VideoController,currentverticalresolution,1)
n83=  var %Bits $WbemGet(Win32_VideoController,currentbitsperpixel,1)
n84=  var %Refresh $WbemGet(Win32_VideoController,currentrefreshrate,1)
n85=  if ($com(Wbem.2)) { .comclose Wbem.2 }  
n86=  $iif($isid,return,$iif(# ischan,say,echo -a)) 04V14ideo:04 %Compatibility %VideoProcessor $+($bytes(%AdapterRam,3),MB) $+(%Horizontal,x,%Vertical) $+(%Bits,bit) $+(%Refresh,Hz)
n87=}
n88=alias -l DiskInfo {
n89=  var %d $disk(0),%total 0,%free 0,%result
n90=  while (%d) {
n91=    if ($disk(%d).size) {
n92=      %total = $calc(%total + $disk(%d).size)
n93=      %free = $calc(%free + $disk(%d).free)
n94=      %result = %result 04I14D:04 $disk(%d).label $disk(%d).path 04T14ype:04 $disk(%d).type 04S14ize:04 $bytes($disk(%d).size,g3).suf 04F14ree:04 $bytes($disk(%d).free,g3).suf 
n95=    }
n96=    dec %d 
n97=  }
n98=  $iif($isid,return,$iif(# ischan,say,echo -a)) %Result 04T14otal 04D14isk 04S14pace:04 $bytes(%Total,g3).suf 04T14otal 04S14pace 04F14ree:04 $bytes(%free,g3).suf 
n99=}  
n100=alias -l WbemOpen {
n101=  .comopen Wbem.1 WbemScripting.SWbemLocator
n102=  .comclose Wbem.1 $com(Wbem.1,ConnectServer,3,dispatch* Wbem.2)
n103=}
n104=alias -l WbemGet {
n105=  if ($com(Wbem.3)) { .comclose Wbem.3 }
n106=  if ($com(Wbem.2,ExecQuery,3,bstr*,select $2 from $1,dispatch* Wbem.3)) { var %Result $comval(Wbem.3,$3,$2) }
n107=  .comclose Wbem.3 
n108=  return %Result
n109=}
